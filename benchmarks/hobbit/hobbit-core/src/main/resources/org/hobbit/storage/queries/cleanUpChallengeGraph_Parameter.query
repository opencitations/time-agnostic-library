PREFIX hobbit: <http://w3id.org/hobbit/vocab#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

WITH %GRAPH_URI%
DELETE {
    ?parameter	?parameterProp	?parameterObject .
    ?parameterRange	?parameterRangeProp	?parameterRangeObject .
    ?parameterInstance	?parameterInstanceProp	?parameterInstanceObject .
}
WHERE {
    {?parameter a hobbit:Parameter} UNION {?parameter a hobbit:ConfigurableParameter} UNION {?parameter a hobbit:FeatureParameter} .
    ?parameter	?parameterProp	?parameterObject .
    FILTER NOT EXISTS {
        ?challengeTask a hobbit:ChallengeTask .
        ?challengeTask hobbit:involvesBenchmark ?otherBenchmark .
        ?otherBenchmark	hobbit:hasParameter ?parameter .
    }
    FILTER NOT EXISTS {
        ?challengeTask a hobbit:ChallengeTask .
        ?challengeTask hobbit:involvesSystemInstance ?otherSysInstance .
        ?otherSysInstance	hobbit:instanceOf ?otherSystem .
        ?otherSystem	hobbit:hasParameter ?parameter .
    }
    OPTIONAL {
        ?parameter	rdfs:range	?parameterRange .
        OPTIONAL  {
            {?parameterRange a rdfs:Class} UNION {?parameterRange a owl:Class} .
            ?parameterRange	?parameterRangeProp	?parameterRangeObject .
            FILTER NOT EXISTS {
                ?challengeTask a hobbit:ChallengeTask .
                ?challengeTask hobbit:involvesBenchmark ?otherBenchmark .
                ?otherBenchmark	hobbit:hasParameter ?otherParameter .
                ?otherParameter	rdfs:range	?parameterRange .
            }
            FILTER NOT EXISTS {
                ?challengeTask a hobbit:ChallengeTask .
                ?challengeTask hobbit:involvesSystemInstance ?otherSysInstance .
                ?otherSysInstance	hobbit:instanceOf ?otherSystem .
                ?otherSystem	hobbit:hasParameter ?otherParameter .
                ?otherParameter	rdfs:range	?parameterRange .
            }
            OPTIONAL  {
                ?parameterInstance	a	?parameterRange .
                ?parameterInstance	?parameterInstanceProp	?parameterInstanceObject .
            }
        }
    }
}
