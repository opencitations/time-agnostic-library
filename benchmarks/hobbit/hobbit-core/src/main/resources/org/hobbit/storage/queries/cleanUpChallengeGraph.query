PREFIX hobbit: <http://w3id.org/hobbit/vocab#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

WITH %GRAPH_URI%
DELETE {
    ?benchmark	a	hobbit:Benchmark ;
                rdfs:label	?benchLabel ;
                rdfs:comment	?benchDesc ;
                hobbit:imageName	?benchImage ;
                hobbit:measuresKPI	?benchKpi ;
                hobbit:hasAPI	?benchApi ;
                hobbit:hasParameter	?benchPara ;
                hobbit:version	?benchVersion .
    ?kpiProp	a	hobbit:KPI ;
                rdfs:label	?kpiLabel ;
                rdfs:comment	?kpiDesc ;
                rdfs:range	?kpiRange .
    ?api		a	hobbit:API ;
                rdfs:label	?apiLabel ;
                rdfs:comment	?apiDesc .
    ?parameter	a	hobbit:Parameter ;
                a	hobbit:ConfigurableParameter ;
                a	hobbit:FeatureParameter ;
                rdfs:label	?parameterLabel ;
                rdfs:comment	?parameterDesc ;
                rdfs:range	?parameterRange ;
                hobbit:defaultValue	?parameterDefault .
    ?parameterRange a	rdfs:Class ;
                    a	owl:Class ;
                    rdfs:label	?parameterRangeLabel ;
                    rdfs:comment	?parameterRangeDesc .
    ?parameterInstance	a	?parameterRange ;
                        rdfs:label	?parameterInstanceLabel ;
                        rdfs:comment	?parameterInstanceDesc .
    ?systemInstance	a	hobbit:SystemInstance ;
                    rdfs:label	?systemLabel ;
                    rdfs:comment	?systemDesc ;
                    hobbit:imageName	?systemImage ;
                    hobbit:implementsAPI	?systemApi ;
                    hobbit:instanceOf	?sysInsSystem ;
                    ?systemParameter ?sysParaValue .
    ?system		a	hobbit:System ;
                hobbit:hasParameter	?systemPara .
}
WHERE {
    OPTIONAL {
        ?benchmark	a	hobbit:Benchmark .
        FILTER NOT EXISTS {
            ?challengeTask a hobbit:ChallengeTask .
            ?challengeTask hobbit:involvesBenchmark ?benchmark .
        }
        OPTIONAL {?benchmark	rdfs:label	?benchLabel .}
        OPTIONAL {?benchmark	rdfs:comment	?benchDesc .}
        OPTIONAL {?benchmark	hobbit:imageName	?benchImage .}
        OPTIONAL {?benchmark	hobbit:version	?benchVersion .}
        OPTIONAL {?benchmark	hobbit:measuresKPI	?benchKpi .}
        OPTIONAL {?benchmark	hobbit:hasAPI	?benchApi .}
        OPTIONAL {?benchmark	hobbit:hasParameter	?benchPara .}
    }
    OPTIONAL {
        {?benchmark	hobbit:measuresKPI	?kpiProp} UNION {?kpiProp	a	hobbit:ConfigurableParameter} .
        FILTER NOT EXISTS {
            ?challengeTask a hobbit:ChallengeTask .
            ?challengeTask hobbit:involvesBenchmark ?otherBenchmark .
            ?otherBenchmark	hobbit:measuresKPI	?kpiProp .
        }
        OPTIONAL {?kpiProp	rdfs:label	?kpiLabel .}
        OPTIONAL {?kpiProp	rdfs:comment	?kpiDesc .}
        OPTIONAL {?kpiProp	rdfs:range	?kpiRange .}
    }
    OPTIONAL {
        {?apiBenchmark	hobbit:hasAPI	?api} UNION {?apiSysIns	hobbit:implementsAPI	?api} UNION {?api	a	hobbit:API} .
        FILTER NOT EXISTS {
            ?challengeTask	a	hobbit:ChallengeTask .
            ?challengeTask	hobbit:involvesBenchmark	?apiBenchmark .
            ?apiBenchmark	hobbit:hasAPI	?api .
        }
        FILTER NOT EXISTS {
            ?challengeTask	a	hobbit:ChallengeTask .
            ?challengeTask	hobbit:involvesSystemInstance	?apiSysIns .
            ?apiSysIns	hobbit:implementsAPI	?api .
        }
        OPTIONAL {?api	rdfs:label	?apiLabel .}
        OPTIONAL {?api	rdfs:comment	?apiDesc .}
    }
    OPTIONAL {
        {?parameter a hobbit:Parameter} UNION {?parameter a hobbit:ConfigurableParameter} UNION {?parameter a hobbit:FeatureParameter} UNION {?otherBenchmark	hobbit:hasParameter ?parameter} UNION {?otherSystem	hobbit:hasParameter ?parameter} .
        FILTER NOT EXISTS {
            ?challengeTask a hobbit:ChallengeTask .
            ?challengeTask hobbit:involvesBenchmark ?otherBenchmark .
            ?otherBenchmark	hobbit:hasParameter ?parameter .
        }
        FILTER NOT EXISTS {
            ?challengeTask a hobbit:ChallengeTask .
            ?challengeTask hobbit:involvesSystemInstance ?otherSysInstance .
            ?otherSysInstance	hobbit:instanceOf ?otherSystem .
            ?otherSystem	hobbit:hasParameter ?parameter .
        }
        OPTIONAL {?parameter	rdfs:label	?parameterLabel .}
        OPTIONAL {?parameter	rdfs:comment	?parameterDesc .}
        OPTIONAL {?parameter	hobbit:defaultValue	?parameterDefault .}
        OPTIONAL {
            ?parameter	rdfs:range	?parameterRange .
            OPTIONAL  {
                {?parameterRange a rdfs:Class} UNION {?parameterRange a owl:Class} .
                FILTER NOT EXISTS {
                    ?challengeTask a hobbit:ChallengeTask .
                    ?challengeTask hobbit:involvesBenchmark ?otherBenchmark .
                    ?otherBenchmark	hobbit:hasParameter ?otherParameter .
                }
                FILTER NOT EXISTS {
                    ?challengeTask a hobbit:ChallengeTask .
                    ?challengeTask hobbit:involvesSystemInstance ?otherSysInstance .
                    ?otherSysInstance	hobbit:instanceOf ?otherSystem .
                    ?otherSystem	hobbit:hasParameter ?otherParameter .
                    ?otherParameter	rdfs:range	?parameterRange .
                }
                OPTIONAL {?parameterRange	rdfs:label	?parameterRangeLabel .}
                OPTIONAL {?parameterRange	rdfs:comment	?parameterRangeDesc .}
                OPTIONAL  {
                    ?parameterInstance	a	?parameterRange .
                    OPTIONAL {?parameterInstance	rdfs:label	?parameterInstanceLabel .}
                    OPTIONAL {?parameterInstance	rdfs:comment	?parameterInstanceDesc .}
                }
            }
        }
    }
    OPTIONAL {
        ?systemInstance	a	hobbit:SystemInstance .
        FILTER NOT EXISTS {
            ?challengeTask	a	hobbit:ChallengeTask .
            ?challengeTask	hobbit:involvesSystemInstance	?systemInstance .
        }
        OPTIONAL {?systemInstance	rdfs:label	?systemLabel .}
        OPTIONAL {?systemInstance	rdfs:comment	?systemDesc .}
        OPTIONAL {?systemInstance	hobbit:imageName	?systemImage .}
        OPTIONAL {?systemInstance	hobbit:implementsAPI	?systemApi .}
        OPTIONAL {?systemInstance	hobbit:instanceOf	?sysInsSystem .}
        OPTIONAL {
            ?systemInstance	hobbit:instanceOf	?sysInsSystem .
            ?sysInsSystem	hobbit:hasParameter	?systemParameter .
            ?systemInstance	?systemParameter ?sysParaValue .
        }
    }
    OPTIONAL {
        {?system	a	hobbit:System} UNION {?someSystemInstance	hobbit:instanceOf	?system .} .
        FILTER NOT EXISTS {
            ?challengeTask	a	hobbit:ChallengeTask .
            ?challengeTask	hobbit:involvesSystemInstance	?systemInstance .
            ?systemInstance	hobbit:instanceOf	?system .
        }
        OPTIONAL {?system	hobbit:hasParameter	?systemPara .}
    }
}
